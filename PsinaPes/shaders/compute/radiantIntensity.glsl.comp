#version 450 core
layout(binding = 0) uniform samplerCube environmentMap;
layout(binding = 1) writeonly uniform imageCube dstLayer;

const float pi = 3.141592;

vec3 CubeTexelToDir(uvec3 texel)
{	
	vec2 faceCoords = texel.xy / vec2(imageSize(dstLayer));
	vec2 centerOriginFaceCoords = faceCoords * 2. - 1.;
	
	vec3 dir;
		
	if(texel.z == 0)      dir = vec3(1.0,  -centerOriginFaceCoords.y, -centerOriginFaceCoords.x);
    else if(texel.z == 1) dir = vec3(-1.0, -centerOriginFaceCoords.y, centerOriginFaceCoords.x);
    else if(texel.z == 2) dir = vec3(centerOriginFaceCoords.x, 1.0, centerOriginFaceCoords.y);
    else if(texel.z == 3) dir = vec3(centerOriginFaceCoords.x, -1.0, -centerOriginFaceCoords.y);
    else if(texel.z == 4) dir = vec3(centerOriginFaceCoords.x, -centerOriginFaceCoords.y, 1.0);
    else if(texel.z == 5) dir = vec3(-centerOriginFaceCoords.x, -centerOriginFaceCoords.y, -1.0);
	
	return normalize(dir);
}
vec2 CartesianToSpherical(vec3 dir)
{
	return vec2(atan(dir.y, dir.x), asin(dir.z));
}
vec3 SphericalToCartesian(vec2 dir)
{	
	return vec3(cos(dir.x) * cos(dir.y), sin(dir.x) * cos(dir.y), sin(dir.y));
}

layout(push_constant) uniform PushConstants
{	
	float theta;
} pushConstants;

layout(local_size_x=32, local_size_y=32, local_size_z=1) in;
void main()
{
	vec3 Center = CubeTexelToDir(gl_GlobalInvocationID);
	
	vec3 Anchor1 = vec3(0, 0, 1);
	vec3 Anchor2 = -Anchor1;
	
	vec3 Anchor = dot(Center, Anchor1) > 0 ? Anchor1 : Anchor2;	
	
	vec3 Right = normalize(cross(Center, Anchor));
	vec3 Up = cross(Center, Right);
	
	mat3 LocalToWorld = mat3(Center, Right, Up);
	
	
	float cosTheta = cos(pushConstants.theta);
	float cosThetaBias = 1e-4;
	
	uint samples = 32;
	vec3 integral = vec3(0);
	float dtheta = pushConstants.theta * 2 / float(samples);
	
	float sampleCount = 0;
	
	for(float azimuth = -pushConstants.theta; azimuth <= pushConstants.theta; azimuth += dtheta)
	{
		for(float zenith = -pushConstants.theta; zenith <= pushConstants.theta; zenith += dtheta)
		{
			vec3 LocalWi = SphericalToCartesian(vec2(azimuth, zenith));
			vec3 Wi = LocalToWorld * LocalWi;
			
			if(dot(Center, Wi) + cosThetaBias >= cosTheta)
			{
				integral += texture(environmentMap, Wi).rgb;
				sampleCount += 1;
			}
		}
	}
	
	uvec2 size = imageSize(dstLayer);
	//if(gl_GlobalInvocationID.x < size.x && gl_GlobalInvocationID.y < size.y) imageStore(dstLayer, ivec3(gl_GlobalInvocationID), vec4(I, 1.0));
	if(gl_GlobalInvocationID.x < size.x && gl_GlobalInvocationID.y < size.y) imageStore(dstLayer, ivec3(gl_GlobalInvocationID), vec4(integral / sampleCount, 1.0));
}